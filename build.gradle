plugins {
  id 'com.jfrog.bintray' version '0.6'
  id 'com.github.hierynomus.license' version '0.11.0'
}

group = 'com.github.rafael-brandao.gradle'
version = '0.1.0-SNAPSHOT'
description = 'Gradle plugin for using Google Protocol Buffers in your Scala project.'
status = { "$version".endsWith('-SNAPSHOT') ? 'integration' : 'release' }()


apply plugin: 'idea'
apply plugin: 'scala'
apply plugin: 'java-gradle-plugin'
apply from: script('license')

ext {
  scalaVersion = '2.11.4'
  scalaBase = '2.11'
  pluginId = 'com.github.rafael-brandao.scalabuff'
}

repositories {
  jcenter()
}

dependencies {
  compile localGroovy()
  compile "org.scala-lang:scala-library:$scalaVersion"
  testCompile(
      "org.scalatest:scalatest_$scalaBase:2.2.3-SNAP2",
      'junit:junit:4.12-beta-2'
  )
}

test {
  jvmArgs '-Xms256m', '-Xmx512m'
  testLogging.with {
    exceptionFormat = 'full'
  }
}

tasks.withType(ScalaCompile).each { task ->
  task.with {
    options.encoding = 'UTF-8'
    scalaCompileOptions.with {
      additionalParameters = ['-feature', '-Yinline-warnings']
      deprecation = true
      encoding = 'UTF-8'
      useAnt = false
    }
  }
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource.srcDirs
}

artifacts {
  archives sourcesJar
}

task wrapper(type: Wrapper) {
  gradleVersion = "2.1"
}

apply from: script('publishing')

// Helper methods
File script(String name) {
  project.file("gradle/${name}.gradle")
}
