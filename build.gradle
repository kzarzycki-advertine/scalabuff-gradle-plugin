plugins {
    id 'com.jfrog.bintray' version '0.6'
    id 'com.github.hierynomus.license' version '0.11.0'
}

group = 'com.github.rafael-brandao.gradle'
version = '0.1.1-SNAPSHOT'
description = 'Gradle plugin for using Google Protocol Buffers in your Scala project.'
status = { "$version".endsWith('-SNAPSHOT') ? 'integration' : 'release' }()


apply plugin: 'idea'
apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin'
apply from: script('license')

ext {
    pluginId = 'com.github.rafael-brandao.scalabuff'
}

repositories {
    jcenter()
}

dependencies {
    compile localGroovy()
    compile('com.github.mperry:functionalgroovy-core:0.6') {
        exclude module: 'groovy-all'
        exclude module: 'junit'
    }

    testCompile('org.spockframework:spock-core:0.7-groovy-2.0') {
        exclude module: 'groovy-all'
    }
}

test {
    jvmArgs '-Xms256m', '-Xmx512m'
    testLogging.with {
        exceptionFormat = 'full'
    }
}

tasks.withType(GroovyCompile).each { task ->
    task.with {
        options.encoding = 'UTF-8'
        groovyOptions.encoding = 'UTF-8'
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource.srcDirs
}

artifacts {
    archives sourcesJar
}

task wrapper(type: Wrapper) {
    gradleVersion = "2.2"
    distributionUrl = "https://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}

apply from: script('publishing')

// Helper methods
File script(String name) {
    project.file("gradle/${name}.gradle")
}
